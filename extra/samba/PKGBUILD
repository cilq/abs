# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>
# Contributor: Marco A Rojas <marquicus at gmail.com>
# Contributor: Netanel Shine <netanel at archlinux.org.il >
# Contributor: ngoonee <ngoonee.talk@gmail.com>
# Contributor: Adam Russell <adamlr6+arch@gmail.com>
# Contributor: Dhananjay Sathe <dhananjaysathe@gmail.com>
# Contributor: Kim Carlb√§cker <kim.carlbacker@gmail.com>

pkgbase=samba
pkgname=('libwbclient' 'smbclient' 'samba')
pkgver=4.1.8
# We use the 'A' to fake out pacman's version comparators.  Samba chooses
# to append 'a','b',etc to their subsequent releases, which pamcan
# misconstrues as alpha, beta, etc.  Bad samba!
_realver=4.1.8
pkgrel=4
arch=(i686 x86_64)
url="http://www.samba.org"
license=('GPL3')
makedepends=('python2' 'docbook-xsl' 'pkg-config' 'db' 'popt' 'libcups' 
             'tevent' 'libldap' 'ldb>=1.1.15' 
             'gamin' 'gnutls>=2.4.1' 'talloc' 'tdb' 'dbus')
port_patches=(patch-buildtools__scripts__abi_gen.sh
              patch-buildtools__wafsamba__samba_install.py
              patch-docs-xml__wscript_build
              patch-lib__replace__wscript
              patch-lib__replace__xattr.c
              patch-nsswitch__winbind_nss_freebsd.c
              patch-nsswitch__wins_freebsd.c
              patch-source3__client__dnsbrowse.c
              patch-source3__lib__sysquotas_nfs.c
              patch-source3__smbd__quotas.c
              patch-source3__utils__net.c
              patch-source3__utils__net_time.c
              patch-source3__winbindd__wscript_build
              patch-source3__wscript
              patch-source3__wscript_build
              patch-source4__dns_server__dns_crypto.c
              patch-source4__heimdal__lib__roken__rkpty.c
              patch-source4__ntvfs__sysdep__wscript_build
              patch-source4__ntvfs__sysdep__wscript_configure
              patch-wscript patch-wscript_build
)
source=(http://us1.samba.org/samba/ftp/stable/${pkgbase}-${_realver}.tar.gz
        samba.logrotate
        swat.xinetd
        samba.pam
        samba-init.d
        samba.in
	${port_patches[@]})
### UNINSTALL dmapi package before building!!!

prepare() {
  cd ${srcdir}/samba-${_realver}

  for _patch in ${port_patches[@]}; do
    msg "Patching port patch $_patch"
    patch -p0 -i "${srcdir}/$_patch"
  done

  sed -i '' -e "s|%%SAMBA4_CONFDIR%%|/etc/samba|g" \
            -e "s|%%SAMBA4_CONFIG%%|smb.conf|g" \
            -e "s|%%PREFIX%%|/usr|g" \
            -e "s|sbin|bin|g" \
            ${srcdir}/samba.in
}
build() {
  # Use samba-pkg as a staging directory for the split packages
  # (This is so RPATHS and symlinks are generated correctly via
  # make install, but the otherwise unsplit pieces can be split)
  _pkgsrc=${srcdir}/samba-pkg
  rm -rf ${_pkgsrc}
  cd ${srcdir}/samba-${_realver}
  # change to use python2
  SAVEIFS=${IFS}
  IFS=$(echo -en "\n\b")
  PYTHON_CALLERS="$(find ${srcdir}/samba-${_realver} -name '*.py')
$(find ${srcdir}/samba-${_realver} -name 'wscript*')
$(find ${srcdir}/samba-${_realver} -name 'configure.ac')
$(find ${srcdir}/samba-${_realver} -name 'upgrade_from_s3')
$(find ${srcdir}/samba-${_realver}/buildtools -type f)
$(find ${srcdir}/samba-${_realver}/source4/scripting -type f)"
  gsed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
         -e "s|python-config|python2-config|" \
         -e "s|bin/python|bin/python2|" \
      ${PYTHON_CALLERS}
  IFS=${SAVEIFS}

  export PYTHON=/usr/bin/python2
_samba4_idmap_modules=idmap_ad,idmap_rid,idmap_adex,idmap_hash,idmap_tdb2
_samba4_pdb_modules=pdb_tdbsam,pdb_ldap,pdb_ads,pdb_smbpasswd,pdb_wbc_sam,pdb_samba4
_samba4_auth_modules=auth_unix,auth_wbc,auth_server,auth_netlogond,auth_script,auth_samba4
  cd ${srcdir}/samba-${_realver}
  ./configure --enable-fhs \
              --prefix=/usr \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-configdir=/etc/samba \
              --with-lockdir=/var/cache/samba \
              --with-sockets-dir=/var/run/samba \
              --with-piddir=/var/run \
              --with-ads \
              --with-ldap \
              --with-winbind \
              --with-acl-support \
              --enable-gnutls \
              --with-pam \
              --with-pammodulesdir=/usr/lib/security \
              --with-shared-modules=${_samba4_idmap_modules},${_samba4_pdb_modules},${_samba4_auth_modules} \
              --disable-rpath-install

              # Add this to the options once it's working...
               #--with-system-mitkrb5 /opt/heimdal
  gmake
  gmake DESTDIR="${_pkgsrc}/" install

  # This gets skipped somehow
  if [ ! -e ${_pkgsrc}/usr/bin/smbtar ]; then
      install -m755 ${srcdir}/samba-${_realver}/source3/script/smbtar ${_pkgsrc}/usr/bin/
  fi
}

package_libwbclient() {
pkgdesc="Samba winbind client library"
depends=()
  ### TO REPACKAGE:
  #_pkgsrc=${srcdir}/samba-pkg
  #rm -rf ${_pkgsrc}
  #cd ${srcdir}/samba-${_realver}
  #export PYTHON=/usr/bin/python2
  #gmake DESTDIR="${_pkgsrc}/" install
  #cd "$srcdir"
  # Use samba-pkg as a staging directory for the split packages
  # (This is so RPATHS and symlinks are generated correctly via
  # make install, but the otherwise unsplit pieces can be split)
  _pkgsrc=${srcdir}/samba-pkg
  install -d -m755 ${pkgdir}/usr/lib
  mv ${_pkgsrc}/usr/lib/libwbclient*.so* ${pkgdir}/usr/lib/

  install -d -m755 ${pkgdir}/usr/lib/samba
  mv ${_pkgsrc}/usr/lib/samba/libwinbind-client*.so* ${pkgdir}/usr/lib/samba/
  mv ${_pkgsrc}/usr/lib/samba/libreplace.so* ${pkgdir}/usr/lib/samba/

  install -d -m755 ${pkgdir}/usr/lib/pkgconfig
  mv ${_pkgsrc}/usr/lib/pkgconfig/wbclient.pc ${pkgdir}/usr/lib/pkgconfig/

  install -d -m755 ${pkgdir}/usr/include/samba-4.0
  mv ${_pkgsrc}/usr/include/samba-4.0/wbclient.h ${pkgdir}/usr/include/samba-4.0/
}

package_smbclient() {
pkgdesc="Tools to access a server's filespace and printers via SMB"
depends=('popt' 'tdb' "libwbclient>=$pkgver" 'ldb'
         'tevent' 'libgcrypt' 'python2' 'talloc' 'gnutls' 
         'libldap' 'libcups')

    _smbclient_bins=('smbclient' 'smbclient4' 'rpcclient' 'smbspool'
                     'smbtree' 'smbcacls' 'smbcquotas' 'smbget' 'net'
                     'nmblookup' 'nmblookup4' 'smbtar')
    # Use samba-pkg as a staging directory for the split packages
    # (This is so RPATHS and symlinks are generated correctly via
    # make install, but the otherwise unsplit pieces can be split)
    _pkgsrc=${srcdir}/samba-pkg
    install -d -m755 ${pkgdir}/usr/bin
    for bin in ${_smbclient_bins[@]}; do
        mv ${_pkgsrc}/usr/bin/${bin} ${pkgdir}/usr/bin/
    done

    # smbclient binaries link to the majority of the samba
    # libs, so this is a shortcut instead of resolving the
    # whole dependency tree by hand
    install -d -m755 ${pkgdir}/usr/lib
    for lib in ${_pkgsrc}/usr/lib/lib*.so*; do
        mv ${lib} ${pkgdir}/usr/lib/
    done

    install -d -m755 ${pkgdir}/usr/lib/samba
    for lib in ${_pkgsrc}/usr/lib/samba/lib*.so*; do
        mv ${lib} ${pkgdir}/usr/lib/samba/
    done

    install -d -m755 ${pkgdir}/usr/lib/pkgconfig
    mv ${_pkgsrc}/usr/lib/pkgconfig/smbclient.pc ${pkgdir}/usr/lib/pkgconfig/
    mv ${_pkgsrc}/usr/lib/pkgconfig/smbclient-raw.pc ${pkgdir}/usr/lib/pkgconfig/
    mv ${_pkgsrc}/usr/lib/pkgconfig/netapi.pc ${pkgdir}/usr/lib/pkgconfig/

    install -d -m755 ${pkgdir}/usr/share/man/man1
    install -d -m755 ${pkgdir}/usr/share/man/man7
    install -d -m755 ${pkgdir}/usr/share/man/man8
    for bin in ${_smbclient_bins[@]}; do
        if [ -e ${_pkgsrc}/usr/share/man/man1/${bin}.1 ]; then
            mv ${_pkgsrc}/usr/share/man/man1/${bin}.1 ${pkgdir}/usr/share/man/man1/
        fi
        if [ -e ${_pkgsrc}/usr/share/man/man8/${bin}.8 ]; then
            mv ${_pkgsrc}/usr/share/man/man8/${bin}.8 ${pkgdir}/usr/share/man/man8/
        fi
    done
    # missing: mv ${_pkgsrc}/usr/share/man/man7/libsmbclient.7 ${pkgdir}/usr/share/man/man7/

    install -d -m755 ${pkgdir}/usr/include/samba-4.0
    mv ${_pkgsrc}/usr/include/samba-4.0/libsmbclient.h ${pkgdir}/usr/include/samba-4.0/
    mv ${_pkgsrc}/usr/include/samba-4.0/netapi.h ${pkgdir}/usr/include/samba-4.0/

    mkdir -p ${pkgdir}/usr/lib/cups/backend
    ln -sf /usr/bin/smbspool ${pkgdir}/usr/lib/cups/backend/smb
}

package_samba() {
pkgdesc="SMB Fileserver and AD Domain server"
depends=('db>=4.7' 'popt' 'libcups' 'gamin' 'gnutls>=2.4.1'
         'talloc' 'ldb' 'python2' 'tdb' "smbclient>=$pkgver")
backup=(etc/logrotate.d/samba
        etc/pam.d/samba
        etc/samba/smb.conf
        etc/xinetd.d/swat)
    # Use samba-pkg as a staging directory for the split packages
    # (This is so RPATHS and symlinks are generated correctly via
    # make install, but the otherwise unsplit pieces can be split)
    _pkgsrc=${srcdir}/samba-pkg
    # Everything that libwbclient and smbclient didn't install goes
    # into the samba package...
    mv ${_pkgsrc}/* ${pkgdir}/
    rmdir ${_pkgsrc}

    _pyver=`python2 -c 'import sys; print(sys.version[:3])'`

    find ${pkgdir}/usr/lib/python${_pyver}/site-packages/ -name '*.py' | \
         xargs gsed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
    find ${pkgdir}/usr/bin ${pkgdir}/usr/bin -type f | \
         xargs gsed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"

    # Make admin scripts look in the right place for the samba python module
    for script in bin/samba_dnsupdate bin/samba_kcc bin/samba_spnupdate \
                  bin/samba_upgradedns bin/samba-tool
    do
        gsed -i "/^sys\.path\.insert/ a\
sys.path.insert(0, '/usr/lib/python${_pyver}/site-packages')" \
               ${pkgdir}/usr/${script}
    done

  # install systemd files
  install -d -m755 ${pkgdir}/etc/{init.d,rc.d}

  install -m755 ${srcdir}/samba.in ${pkgdir}/etc/rc.d/samba
  install -m644 ${srcdir}/samba-init.d ${pkgdir}/etc/init.d/samba
  #done

  # install sample smb.conf
  install -d -m755 ${pkgdir}/etc/samba
  install -m644 ${srcdir}/samba-${_realver}/packaging/LSB/smb.conf ${pkgdir}/etc/samba/smb.conf.default
  
  mkdir -p ${pkgdir}/etc/samba/private
  chmod 700 ${pkgdir}/etc/samba/private

  install -d -m755  ${pkgdir}/etc/{logrotate.d,xinetd.d,pam.d}
  install -m644 ${srcdir}/samba.logrotate ${pkgdir}/etc/logrotate.d/samba
  install -m644 ${srcdir}/swat.xinetd ${pkgdir}/etc/xinetd.d/swat
  install -m644 ${srcdir}/samba.pam ${pkgdir}/etc/pam.d/samba

  # winbind krb5 locator
  mkdir -p ${pkgdir}/usr/lib/krb5/plugins/libkrb5
  mv  ${pkgdir}/usr/lib/*.so ${pkgdir}/usr/lib/krb5/plugins/libkrb5/

  # fix logrotate
  gsed -i -e 's|log.%m|%m.log|g' ${pkgdir}/etc/samba/smb.conf.default
  
  # spool directory
  install -d -m1777 ${pkgdir}/var/spool/samba
  
  rm -rf ${pkgdir}/var/run
  
  # copy ldap example
  install -dm755 ${pkgdir}/usr/share/doc/samba/examples/LDAP
  install -m644 ${srcdir}/samba-${_realver}/examples/LDAP/samba.schema ${pkgdir}/usr/share/doc/samba/examples/LDAP/samba.schema
}

md5sums=('212b7568d44e7668a7dc8a45c5558764'
         '5697da77590ec092cc8a883bae06093c'
         'ee8507839745cf17d2fb5401be7da6a8'
         '96f82c38f3f540b53f3e5144900acf17'
         '227565650bcb0bbce2cfb7912a7b0a55'
         'cc2cbb167bc02e5f83bed24b63efddf3'
         'c034717d5d6bca6c3aa8013d4d912c4d'
         '38850a329f5d95ac91b1c6156ed5c425'
         'f7b4f080d0790cd366fa432be850fc78'
         '6037181a5f80b16d0400bc0dbfb85ef6'
         '550c81715694572c90dce7408ecd0268'
         '3cb36512651a19e21f40a180165fa3ac'
         'e82e1f09669ede84816d4b71ac94486e'
         '12d9b786326b0cae9a6567411f63bbb5'
         '1f02f8056a2d39051292695621043fef'
         'a8835b23b2a290cfc3cf9e2f42c33649'
         '6ba1adc5a1221ce8d72b3d0d821713be'
         'aaba7b5d408b88d8905d08311fd2ec0b'
         '2a7e7b0192f1acdeea743b3c519d4181'
         '28e84e89355d4817ab31411fd04b3d4c'
         'fc861231acf7560e0eb2aea1de19cee3'
         '3fcb41a5ff80289d05fe8056a5ff8808'
         '61c353262504bb2067bc671475481633'
         '236dc40a502ed3c0b181f4aad64b1c96'
         'e15cfe19837fb395d6696d90044317bd'
         'db41f4c2bc8cb60da7a98bf0a0dd1ce9'
         '72be6869417b26ed079eee94d91e5851'
         '75ce18b091e82ca402f396510d7882ac')
